cmake_minimum_required (VERSION 3.10)
project ("grx" VERSION 3.2.0 LANGUAGES C)
set (ABI_VERSION 3)
set (GIR_NAME "Grx")
set (GIR_VERSION "3.0")
set (PACKAGE_NAME ${PROJECT_NAME}-${GIR_VERSION})

# Add optimizations (TODO: Get these from an environment variable for compatibility
#set (CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)
#set (CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -s -pipe -Ofast -ffp-contract=fast -ffast-math -funroll-loops -flto -march=armv5tej")
# TODO: Consider logging details when run and using -fbranch-probabilities

# Configuration settings

set (GRX_DEBUG "OFF" CACHE STRING "Debug message flags")
if (CMAKE_HOST_SYSTEM_NAME STREQUAL "Linux")
    set (GRX_PLUGIN_LINUXFB "Yes" CACHE BOOL "Enable Linux framebuffer video driver plugin")
else ()
    set (GRX_PLUGIN_LINUXFB "No")
endif ()
set (GRX_PLUGIN_GTK3 "Yes" CACHE BOOL "Enable GTK+ 3 video driver plugin")
set (GRX_ENABLE_DOC "Yes" CACHE BOOL "Enable building docs")

# we have local cmake modules

list (APPEND CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake)

# macOS/brew stuff
if (APPLE)
    # see `brew info gettext` caveats
    link_directories (/usr/local/opt/gettext/lib)
    include_directories (/usr/local/opt/gettext/include)
    # .typelib does not work without this
    set (CMAKE_MACOSX_RPATH No)
endif (APPLE)

# global stuff

find_package (PkgConfig REQUIRED)
include(GNUInstallDirs)
set (CMAKE_INSTALL_PKGDATADIR ${CMAKE_INSTALL_DATAROOTDIR}/${PACKAGE_NAME})
set (CMAKE_INSTALL_PKGLIBDIR ${CMAKE_INSTALL_LIBDIR}/${PACKAGE_NAME})
set (CMAKE_INSTALL_PKGPLUGINDIR ${CMAKE_INSTALL_PKGLIBDIR}/plugins/${ABI_VERSION})

set (SHARED_LIBRARY_TARGET ${PACKAGE_NAME}-shared)
set (STATIC_LIBRARY_TARGET ${PACKAGE_NAME}-static)
set (GIR_TARGET ${PACKAGE_NAME}-gir)
set (TYPELIB_TARGET ${PACKAGE_NAME}-typelib)

# subdirectories

# src goes first for depedency reasons
add_subdirectory (src)

add_subdirectory (doc)
add_subdirectory (plugins)
add_subdirectory (tools)
add_subdirectory (example)
add_subdirectory (test)

# these should probably be move do subdirectories too

install (FILES bindings/vala/grx-3.0.vapi
    DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/vala/vapi/)

install (FILES include/grx-3.0.h DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
install (DIRECTORY include/grx DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

# exports for locally including this proejct in another cmake project

configure_file (${PACKAGE_NAME}Config.cmake.in ${PACKAGE_NAME}Config.cmake @ONLY)
export (TARGETS ${SHARED_LIBRARY_TARGET} ${STATIC_LIBRARY_TARGET}
    APPEND FILE ${PACKAGE_NAME}Config.cmake)
export (PACKAGE ${PACKAGE_NAME})
